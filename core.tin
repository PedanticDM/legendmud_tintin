#ALIAS {load_user_config}
{
    #SYSTEM touch user.config;
    #LINE {quiet} {#READ user.config};

    #CLASS default_config read defaults.config;

    #CLASS config open;
    #FOREACH {*defaults[%*]} {default}
    {
        #IF {&{$default}} {}
        {
            #LINE sub {var}
            {
                #VAR {$default} {$defaults[$default]};
            }
        }
    }
    #UNVAR default;
    #CLASS config write user.config;
    #CLASS config close;

    #LIST modules create $modules;

    #CLASS default_config kill;
}

#FUNCTION {has_module}
{
    #LIST modules find %1 list_check;
    #LOCAL check $list_check;
    #UNVAR list_check;
    #RETURN $check;
}

#EVENT {PROGRAM START}
{
    #LINE quiet {#READ sys.config};
    load_user_config;
    #SESSION legendmud mud.legendmud.org 9999;
}

#EVENT {SCREEN RESIZE}
{
    #NOP Hack to fix the weird behavior in latest version of tintin.;
    #SPLIT $split_top $split_bottom;
}

#EVENT {SESSION CONNECTED}
{
	#REGEX {%0} {^legendmud$}
    {
        #IF {"$autologin" == "true"}
        {
            #SEND $login_name;
            #SEND $login_password;
        };

        #UNVAR autologin;
        #UNVAR login_name;
        #UNVAR login_password;

        #FOREACH {$modules[%*]} {module}
        {
            #READ modules/$module.tin;
        };
        #UNVAR module;
        
        #DELAY {.25}
        {
            #SPLIT $split_top $split_bottom;
        }
    };
}

#PATHDIR          {d}  {u}  {32}
#PATHDIR          {e}  {w}  {2}
#PATHDIR          {n}  {s}  {1}
#PATHDIR          {ne}  {sw}  {3}
#PATHDIR          {nw}  {se}  {9}
#PATHDIR          {s}  {n}  {4}
#PATHDIR          {se}  {nw}  {6}
#PATHDIR          {sw}  {ne}  {12}
#PATHDIR          {u}  {d}  {16}
#PATHDIR          {w}  {e}  {8}

#TICKER           {idle}  {#SEND}  {300}

#ACTION {^Press RETURN to continue.$}
{
	#DELAY {1} {#SEND}
}
{5}

#ACTION {{^NOTE: ([\w\-\'\.]+) is a member of your account and is in-game but disconnected.}}
{
	#SEND %2
}
{5}

#ALIAS {loadclass %1}
{
    #SHOWME Attempting to load classes/%1.tin;
    #CLASS {%1} {read} {classes/%1.tin};
}

#ALIAS {killclass %1}
{
    #LINE verbose #CLASS {%1} kill;
}

#ALIAS {jtform %1}
{
    #REGEX {%1} {{(?:\d+\.)?(.*)}}
    {
        #SEND remove %1;
        #VAR tform_kw &2;
        #SEND give $tform_kw old;
        #ACTION {{^A mysterious shugenja gives .*? to you.$}}
        {
            #SEND wear $tform_kw;
            #UNVAR tform_kw;
            #UNACTION {{^A mysterious shugenja gives .*? to you.$}};
        }
    };
}

#VARIABLE         {MAP_LINES}  {22}
#VARIABLE         {MAP_ROWS}  {81}
