#LINE quiet {#READ sys.config};

#FUNCTION {is_out_of_date}
{
    #INFO system save;
    #LOCAL tt_version {$info[SYSTEM][CLIENT_VERSION]};
    #UNVAR info;
    #REPLACE {tt_version} {.} {};
    #REPLACE {tt_version} {b} {};
    #IF {$tt_version < 20190}
    {
        #RETURN 1;
    }
    {
        #RETURN 0;
    };
}

#ALIAS {reload}
{
    #IF {"%1" == "" || "%1" == "all"}
    {
        #SHOWME \nReloading all modules...;
        #LINE quiet {#READ core_module.tin};

        #UNVAR autologin;
        #UNVAR login_name;
        #UNVAR login_password;

        #SHOWME Reload complete.\n;
    }
    {
        #CLASS {module_%1} {kill};
        #CLASS {module_%1} {open};
        #SHOWME \nReloading <069>%1<099>...;
        #READ modules/%1.tin;
        #CLASS {module_%1} {close};
        #SHOWME Reload complete.\n;
    };
};



#EVENT {PROGRAM START}
{
    #IF {@is_out_of_date{}}
    {
        #SHOWME <001>Your version of Tintin++ is out of date.;
        #SHOWME <001>Please upgrade to v2.01.90 or later.;
    }
    {
        #SESSION legendmud mud.legendmud.org 9999;
    };
}

#EVENT {SESSION CONNECTED}
{
    #LINE quiet {#READ core_module.tin};
    #IF {"$autologin" == "true"}
    {
        #ACTION {{^Enter your choice or the name of your character:}}
        {
            #SEND $login_name;
            #SEND $login_password;

            #UNVAR autologin;
            #UNVAR login_name;
            #UNVAR login_password;

            #UNACTION {{^Enter your choice or the name of your character:}}
        }
        {5};
    }
    {
        #UNVAR autologin;
        #UNVAR login_name;
        #UNVAR login_password;
    };
}

#ALIAS {jtform %1}
{
    #REGEX {%1} {{(?:\d+\.)?(.*)}}
    {
        #SEND remove %1;
        #VAR tform_kw &2;
        #SEND give $tform_kw old;
        #ACTION {{^A mysterious shugenja gives .*? to you.$}}
        {
            #SEND wear $tform_kw;
            #UNVAR tform_kw;
            #UNACTION {{^A mysterious shugenja gives .*? to you.$}};
        }
    };
}
