#EVENT {DAY}
{
	#FORMAT date %t %F;
	#LOG APPEND {logs/log_$date\.txt};
    #UNVARIABLE date;
}

#EVENT {MAP ENTER ROOM}
{
    #READ map/map_size.tin;
	#MAP map ${MAP_ROWS}x${MAP_LINES} map/map.txt;
}

#EVENT {PROGRAM START}
{
    #READ config;
	#SESSION legendmud mud.legendmud.org 9999;
}

#EVENT {SESSION CONNECTED}
{
	#REGEX {%0} {^legendmud$}
    {
        #IF {&{login_name}}
        {
            #SEND $login_name;
            #SEND $login_password;
            #UNVARIABLE login_password;
            #UNVARIABLE login_name;
        };

        #MAP read map/legendmud.map;
        #MAP return;

        #FORMAT date %t %F;
        #LOG APPEND {logs/log_$date\.txt};
        #UNVARIABLE date;
        
        #READ prompt.tin;
        #READ combat.tin;
        #READ map/action.tin;
        #READ map/alias.tin;
    };
}

#MACRO {\eOP}
{
	#HELP
}

#MACRO {\eOQ}
{
	#SEND who
}

#MACRO {\eOR}
{
	#SEND group
}

#MACRO {\eOS}
{
	#SEND stat
}

#PATHDIR          {d}  {u}  {32}
#PATHDIR          {e}  {w}  {2}
#PATHDIR          {n}  {s}  {1}
#PATHDIR          {ne}  {sw}  {3}
#PATHDIR          {nw}  {se}  {9}
#PATHDIR          {s}  {n}  {4}
#PATHDIR          {se}  {nw}  {6}
#PATHDIR          {sw}  {ne}  {12}
#PATHDIR          {u}  {d}  {16}
#PATHDIR          {w}  {e}  {8}

#TICKER           {idle}  {#SEND}  {300}

#ACTION {^Press RETURN to continue.$}
{
	#DELAY {1} {#SEND}
}
{5}

#ACTION {{^NOTE: ([\w\-\'\.]+) is a member of your account and is in-game but disconnected.}}
{
	#SEND %2
}
{5}

#ACTION {~{^(?:\e\[[\d\;]{4,6}m)*[<\[]\e\[[\d\;]{4,6}m(?!Exits)(\w+)(?:(?:\e\[[\d\;]{4,6}m)?\:(?:\e\[[\d\;]{4,6}m)?([^>\]]+))?(?:\e\[[\d\;]{4,6}m)?[>\]].*$}}
{
	#FORMAT curtime {%t} {%T};
	#LINE log chat.txt {$curtime %1}
}
{5}

#ACTION {~{\e\[0;33m.*? tell[s]? the group, '.*?$}}
{
	#FORMAT curtime {%t} {%T};
	#LINE log chat.txt {$curtime %1}
}
{5}

#ALIAS {loadclass %1}
{
    #SHOWME Attempting to load classes/%1.tin;
    #CLASS {%1} {read} {classes/%1.tin};
}

#ALIAS {killclass %1}
{
    #LINE verbose #CLASS {%1} kill;
}

#VARIABLE         {MAP_LINES}  {22}
#VARIABLE         {MAP_ROWS}  {81}
#VARIABLE         {oocvnum}  {1}
