#IF {$is_startup}
{
    #VAR phelp_list[map][enmap] {Enables map editing.};
    #VAR phelp_list[map][dismap] {Disabled map editing.};
    #VAR phelp_list[map][rname] {Changes the nickname for a room.};
    #VAR phelp_list[map][savemap] {Saves the map to file.};
    #VAR phelp_list[map][search] {Finds a room based on the specified nickname and teleports you to it.};
    #VAR phelp_list[map][find] {Finds a room based on your current room in the MUD and teleports you to it.};
    #VAR phelp_list[map][nicknames] {Lists all of the rooms that have nicknames};
    #VAR phelp_list[map][walk] {Finds a room based on the specified nickname and walks to it if a path is found.};
    #VAR phelp_list[map][slowwalk] {Finds a room based on the specified nickname and walks *slowly* to it if a path is found.};
    #VAR phelp_list[map][stop] {Stops a walk or slowwalk.};
    #VAR phelp_list[map][door] {Adds a door to the specified exit.};
    #VAR phelp_list[map][sac bunny] {Convenient command to take you through the bunny trans steps after killing the bunny.};
};

#EVENT {MAP ENTER ROOM}
{
    #LINE quiet #READ modules/map/map_size.tin;
    #MAP map ${MAP_LINES} ${MAP_ROWS} overwrite modules/map/map.txt;
}

#EVENT {MAP UPDATED VTMAP}
{
    #IF {&{screen[panes][$pane_map]}}
    {
        #NOP draw box;
        #DRAW box $screen[boxes][$pane_map][y1] $screen[boxes][$pane_map][x1] $screen[boxes][$pane_map][y2] $screen[boxes][$pane_map][x2];

        #LOCAL label_offset {};
        #LOCAL label_end {};
        #MATH label_offset {$screen[boxes][$pane_map][x1] + 2};
        #MATH label_end {$screen[boxes][$pane_map][x1] + 6};
        #DRAW tile $screen[boxes][$pane_map][y1] $label_offset 1 $label_end { MAP };

        #NOP draw room info;
        #MAP get all room_vars;

        #MATH map_info_line {$screen[panes][$pane_map][y2] - 3};

        #FORMAT map_info_text {%s} {  <039>$room_vars[area]<099>};
        #DRAW tile $map_info_line $screen[panes][$pane_map][x1] $map_info_line $screen[panes][$pane_map][x2] {$map_info_text};

        #MATH map_info_line {$map_info_line + 1};
        #FORMAT map_info_text {%s} {  <169>[<fca>$room_vars[vnum]<169>] <129>$room_vars[name]<099>};
        #IF {"$room_vars[note]" != ""}
        {
            #FORMAT map_info_text {%s %s} {$map_info_text} {<169>(<fca>$room_vars[note]<169>)<099>};
        };
        #DRAW tile $map_info_line $screen[panes][$pane_map][x1] $map_info_line $screen[panes][$pane_map][x2] {$map_info_text};

        #MATH map_info_line {$map_info_line + 1};
        #LOCAL map_info_text {  [Exits:};
        #LOCAL count {0};
        #FOREACH *room_vars[exits][%*] {exit}
        {
            #IF {{$count} == {0}}
            {
                #FORMAT map_info_text {%s <499>%s<099>} {$map_info_text} {$exit};
            }
            {
                #FORMAT map_info_text {%s, <499>%s<099>} {$map_info_text} {$exit};
            }
            #MATH count {$count + 1};
        }
        #FORMAT map_info_text {%s]} {$map_info_text};
        #DRAW tile $map_info_line $screen[panes][$pane_map][x1] $map_info_line $screen[panes][$pane_map][x2] {$map_info_text};

        #UNVAR map_info_line;
        #UNVAR map_info_text;
        #UNVAR room_vars;
    };
}

#EVENT {MAP DOUBLE-CLICKED MOUSE BUTTON ONE}
{
    #MAP find %0;
    #PATH run 0.05;
}

#EVENT {MAP SHORT-CLICKED MOUSE BUTTON ONE}
{
    #LOCAL note {};
    #MAP at %0 {#MAP get all room_vars};
    #IF {"$room_vars[note]" != ""}
    {
        #VAR note {<169>(<fca>$room_vars[note]<169>)<099>};
    };
    #SHOWME {\n<039>$room_vars[area]};
    #SHOWME {<169>[<fca>%0<169>] <129>$room_vars[name] $note\n};
    #UNVAR room_vars;
}

#EVENT {MAP LONG-CLICKED MOUSE BUTTON ONE}
{
    #LOCAL note {};
    #MAP at %0 {#MAP get all room_vars};
    #IF {"$room_vars[note]" != ""}
    {
        #VAR note {<169>(<fca>$room_vars[note]<169>)<099>};
    };
    #SHOWME {\n<039>$room_vars[area]};
    #SHOWME {<169>[<fca>%0<169>] <129>$room_vars[name] $note};
    #SHOWME {<179>$room_vars[desc]\n};
    #UNVAR room_vars;
}

#FUNCTION {on_map_resize}
{
    #MAP offset $screen[panes][$pane_map][y1] $screen[panes][$pane_map][x1] $screen[panes][$pane_map][y2] $screen[panes][$pane_map][x2];

    #RETURN #NOP;
}

#MAP read modules/map/legendmud.map;
#MAP return;
#LINE quiet #READ modules/map/map_size.tin;
#MAP map ${MAP_LINES} ${MAP_ROWS} overwrite modules/map/map.txt;

#READ modules/map/action.tin;
#READ modules/map/alias.tin;

#MAP FLAG $map_graphics ON;
#UNVAR map_graphics;

#IF {&{screen[panes][$pane_map]}}
{
    #MAP FLAG vtmap ON;
}
{
    #MAP FLAG vtmap OFF;
}

@on_map_resize{};
