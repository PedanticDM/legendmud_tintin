#ALIAS {dismap}
{
	#LINE verbose #UNVAR editmap;
	#LINE verbose #MAP flag static on;
}
{5}

#ALIAS {enmap}
{
	#LINE verbose #VAR editmap true;
	#LINE verbose #MAP flag static off;
}
{5}

#ALIAS {rname}
{
	#MAP set roomnote
}
{5}

#ALIAS {savemap}
{
    #IF {"$editmap" == "true"}
    {
        #MAP flag static on;
    };
	#LINE verbose #MAP write modules/map/legendmud.map;
    #IF {"$editmap" == "true"}
    {
        #MAP flag static off;
    };
}
{5}

#ALIAS {search %*}
{
	#LINE verbose #MAP list {} {} {} {} {%1} {variable} {search_list};
    #FOREACH {$search_list} {item} {#MAP goto $item;#BREAK;};
    #UNVAR search_list;
    #UNVAR item;
}
{5}

#ALIAS {find}
{
    #VAR findroom true;
    #SEND look;
}
{5}

#ALIAS {nicknames}
{
    #MAP list {roomnote} {{.+}} {variable} {nickname_list};
    #LOOP 1 &nickname_list[] cnt
    {
        #MAP at {*nickname_list[+$cnt]} {#MAP get {roomname} {roomname}};
        #MAP at {*nickname_list[+$cnt]} {#MAP get {roomnote} {roomnote}};
        #FORMAT roomnote {%-10s} {$roomnote};
        #SHOWME $roomnote - $roomname;
    }
    #UNVAR nickname_list;
    #UNVAR roomname;
    #UNVAR roomnote;
    #UNVAR cnt;
}
{5}

#ALIAS {walk %1}
{
	#MAP find {} {roomnote} {%1};
	#PATH run 0.05;
}
{5}

#ALIAS {slowwalk %1}
{
    #MAP find {} {roomnote} {%1};
    #TICK {slowwalk} {#PATH walk f} {0.25};
}
{5}

#EVENT {END OF PATH}
{
    #UNTICK slowwalk;
}

#ALIAS {stop}
{
    #UNTICK slowwalk;
    #SEND clear;
}
{5}

#ALIAS {door %1}
{
    #MAP exit %1 command {open %1;%1};
}
{5}

#ALIAS {sac bunny}
{
    #SEND enq sac bunny;
    #SEND pause 1s;
    #SEND sit circle;
    #SEND enq ponder self;
    #SEND st;
}
