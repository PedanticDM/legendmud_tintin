#FUNCTION {display_chat_log}
{
    #IF {&{screen[panes][$pane_chat]}}
    {
        #NOP draw box;
        #DRAW box $screen[boxes][$pane_chat][y1] $screen[boxes][$pane_chat][x1] $screen[boxes][$pane_chat][y2] $screen[boxes][$pane_chat][x2];

        #LOCAL label_offset {};
        #LOCAL label_end {};
        #MATH label_offset {$screen[boxes][$pane_chat][x1] + 2};
        #MATH label_end {$screen[boxes][$pane_chat][x1] + 7};
        #DRAW tile $screen[boxes][$pane_chat][y1] $label_offset 1 $label_end { CHAT };

        #NOP draw chat;
        #LOCAL chat_height {};
        #LOCAL chat_width {};
        #MATH chat_height {(($screen[panes][$pane_chat][y2] - $screen[panes][$pane_chat][y1]) + 1) * -1};
        #MATH chat_width {$screen[panes][$pane_chat][x2] - $screen[panes][$pane_chat][x1] + 1};

        #LOCAL chat_draw_line {};
        #LOCAL chat_index {};
        #LOCAL count {0};
        #LOCAL chat_split_length {};
        #LOCAL chat_word_wrap {};
        #LOOP -1 $chat_height {chat_message_count}
        {
            #MATH chat_index {$chat_message_count + $chat_offset};
            #FORMAT chat_word_wrap {%+${chat_width}w} {$chat_log[$chat_index]};
            #MATH chat_split_length {&chat_word_wrap[%*] * -1};
            
            #LOOP -1 $chat_split_length {cnt}
            {
                #MATH chat_draw_line {$screen[panes][$pane_chat][y2] + ($count * -1)};
                #IF {$chat_split_length - ($count + $cnt + 1) < $chat_height}
                {
                    #IF {-1 * $count <= $chat_height}
                    {
                        #BREAK;
                    };
                    #FORMAT chat_word_wrap[$cnt] {%+${chat_width}s} { };
                };
                #LINE sub {col;var} #DRAW tile $chat_draw_line $screen[panes][$pane_chat][x1] $chat_draw_line $screen[panes][$pane_chat][x2] {$chat_word_wrap[$cnt]};
                #MATH count {$count + 1};
            }
        };
    };

    #RETURN #NOP;
};

#FUNCTION {add_chat_item}
{
    #LOCAL item {%1};
    #FORMAT item {%t %s} {%T} {$item};
    #LIST chat_log add $item;
	#LINE log chat.txt {$item};

    @display_chat_log{};

    #RETURN #NOP;
};

#FUNCTION {add_test_chat}
{
    #LOOP 1 %1 count
    {
        @add_chat_item{$count};
    }
    #UNVAR count;
};

#ACTION {~{^(?:\e\[[\d\;]{4,6}m)*[<\[]\e\[[\d\;]{4,6}m(?!Exits)(\w+)(?:(?:\e\[[\d\;]{4,6}m)?\:(?:\e\[[\d\;]{4,6}m)?([^>\]]+))?(?:\e\[[\d\;]{4,6}m)?[>\]].*$}}
{
    @add_chat_item{%1};
}
{5}

#ACTION {~{\e\[[\d\;]{4,6}m.*? tell[s]? (?:the group|you), '.*?$}}
{
    @add_chat_item{%1};
}
{5}

#FUNCTION {scroll_chat_up}
{
    #LOCAL chat_height {};
    #MATH chat_height {($screen[panes][$pane_chat][y2] - $screen[panes][$pane_chat][y1])};
    #IF {&chat_log[] < $chat_height}
    {
        #VAR chat_offset 0;
    }
    {
        #MATH chat_offset {@max{$chat_offset + $chat_message_count + 2;(&chat_log[] * -1) + $chat_height + 1}};
    }
    @display_chat_log{};

    #RETURN #NOP;
};

#FUNCTION {scroll_chat_down}
{
    #LOCAL chat_height {};
    #MATH chat_height {($screen[panes][$pane_chat][y2] - $screen[panes][$pane_chat][y1])};
    #MATH chat_offset {$chat_offset - $chat_message_count - 1};
    #MATH chat_offset {@min{0;$chat_offset}};
    @display_chat_log{};

    #RETURN #NOP;
}

#NOP pgup;
#MACRO {\e[5;3~}
{
    @scroll_chat_up{};
}

#MACRO {\e\e[5~}
{
    @scroll_chat_up{};
}

#NOP pgdn;
#MACRO {\e[6;3~}
{
    @scroll_chat_down{};
}

#MACRO {\e\e[6~}
{
    @scroll_chat_down{};
}

#NOP home;
#MACRO {\e\e[1~}
{
    #LOCAL chat_height {};
    #MATH chat_height {($screen[panes][$pane_chat][y2] - $screen[panes][$pane_chat][y1])};
    #IF {&chat_log[] < $chat_height}
    {
        #VAR chat_offset 0;
    }
    {
        #MATH chat_offset {(&chat_log[] * -1) + $chat_height + 1};
    }
    @display_chat_log{};
}

#NOP end;
#MACRO {\e\e[4~}
{
    #MATH chat_offset 0;
    @display_chat_log{};
}

#FUNCTION {on_chat_resize}
{
    #VAR chat_offset 0;
    @display_chat_log{};

    #RETURN #NOP;
}

#NOP create chat log outside of the class so that it doesn't get wiped on reload;
#CLASS module_chat close;
#IF {&{chat_log}} {}
{
    #LIST chat_log create;
};
#CLASS module_chat open;

@on_chat_resize{};
