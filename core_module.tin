#CLASS core kill;
#CLASS core open;
#SHOWME Loading <069>core<099>...;

#FUNCTION {create_layout}
{
    #NOP handle screen layout;
    #IF {&{height_percent}}
    {
        #VAR screen[height_percent] $height_percent;
        #UNVAR height_percent;
    };

    #IF {&{width_percent}}
    {
        #VAR screen[width_percent] $width_percent;
        #UNVAR width_percent;
    };

    #SCREEN get rows screen[height];
    #SCREEN get cols screen[width];
    #MATH screen[height] {$screen[height] - 1};

    #NOP calc scroll region;
    #MATH screen[scroll][y1] {(100 - $screen[height_percent]) * 0.01 * $screen[height]};
    #VAR screen[scroll][x1] {1};
    #VAR screen[scroll][y2] $screen[height];
    #MATH screen[scroll][x2] {$screen[width_percent] * 0.01 * $screen[width]};

    #IF {@has_module{prompt}}
    {
        #MATH screen[scroll][y2] {$screen[scroll][y2] - 3};
    };

    #NOP calc top pane;
    #MATH screen[panes][1][y1] {1};
    #MATH screen[panes][1][x1] {1};
    #MATH screen[panes][1][y2] {$screen[scroll][y1] - 2};
    #MATH screen[panes][1][x2] {$screen[width]};

    #NOP calc top left pane;
    #NOP #MATH screen[panes][1][y1] {1};
    #NOP #MATH screen[panes][1][x1] {1};
    #NOP #MATH screen[panes][1][y2] {$screen[scroll][y1] - 2};
    #NOP #MATH screen[panes][1][x2] {$screen[scroll][x2]};

    #NOP calc top right pane;
    #NOP #MATH screen[panes][2][y1] {1};
    #NOP #MATH screen[panes][2][x1] {$screen[scroll][x2] + 2};
    #NOP #MATH screen[panes][2][y2] {$screen[scroll][y1] - 2};
    #NOP #MATH screen[panes][2][x2] {$screen[width]};

    #NOP calc bot right pane;
    #MATH screen[panes][2][y1] {$screen[scroll][y1]};
    #MATH screen[panes][2][x1] {$screen[scroll][x2] + 2};
    #MATH screen[panes][2][y2] {$screen[height]};
    #MATH screen[panes][2][x2] {$screen[width]};

    #SCREEN scroll $screen[scroll][y1] $screen[scroll][x1] $screen[scroll][y2] $screen[scroll][x2];
    #SCREEN fill split -;
    #BUFFER end;

    #MATH line_start {@max{$screen[panes][2][y1] - 1;1}};
    #MATH line_pos {$screen[panes][2][x1] - 1};
    #DRAW line $line_start $line_pos $screen[height] $line_pos;
    #UNVAR line_pos;
    #UNVAR line_start;

    #IF {$screen[panes][1][y2] > 0}
    {
        #DRAW tile $screen[panes][1][y1] $screen[panes][1][x1] $screen[panes][1][y2] $screen[panes][1][x2];
    };
    #DRAW tile $screen[panes][2][y1] $screen[panes][2][x1] $screen[panes][2][y2] $screen[panes][2][x2];
    #NOP #DRAW tile $screen[panes][3][y1] $screen[panes][3][x1] $screen[panes][3][y2] $screen[panes][3][x2];

    #RETURN #NOP;
};

#FUNCTION {load_user_config}
{
    #CLASS config kill;
    #READ user.config;

    #CLASS default_config read defaults.config;

    #CLASS config open;
    #LOCAL write_default 0;
    #FOREACH {*defaults[%*]} {default}
    {
        #IF {&{$default}} {#NOP;}
        {
            #LOCAL write_default 1;
            #LINE sub {var}
            {
                #VAR {$default} {$defaults[$default]};
            }
        }
    }
    #UNVAR default;
    #IF {$write_default}
    {
        #CLASS config write user.config;
    };
    #CLASS config close;

    #LIST modules create $modules;

    #CLASS default_config kill;

    #RETURN #NOP;
};

#FUNCTION {load_all_modules}
{
    #FOREACH {$modules[%*]} {module}
    {
        #CLASS {module_$module} {kill};
        #CLASS {module_$module} {open};
        #SHOWME Loading <069>$module<099>...;
        #READ modules/$module.tin;
        #CLASS {module_$module} {close};
    };
    #UNVAR module;

    #RETURN #NOP;
};

#FUNCTION {kill_all_modules}
{
    #FOREACH {$modules[%*]} {module}
    {
        #CLASS {module_$module} {kill};
    }
    #UNVAR module;

    #RETURN #NOP;
};

#ALIAS {loadclass %1}
{
    #LIST loaded_classes find %1 loaded_classes_search;
    #IF {$loaded_classes_search > 0}
    {
        #SHOWME <019>Class <069>%1<019> is already loaded.<099>;
    }
    {
        #SHOWME Loading class <069>%1<099>.;
        #LIST loaded_classes add %1;
        #CLASS {%1} {read} {classes/%1.tin};
    }
    #UNVAR loaded_classes_search;
};

#ALIAS {reloadclass %1}
{
    #IF {"%1" == "all"}
    {
        #IF {&{loaded_classes} && &loaded_classes[]}
        {
            #FOREACH {$loaded_classes[%*]} {loaded_classes_class}
            {
                #CLASS {$loaded_classes_class} kill;
                #CLASS {$loaded_classes_class} {read} {classes/$loaded_classes_class.tin};
                
                #SHOWME Class <069>%1<099> has been reloaded.;
            }
            #UNVAR loaded_classes_class;
        }
        {
            #SHOWME <019>No classes are loaded.<099>;
        }
    }
    {
        #LIST loaded_classes find %1 loaded_classes_search;
        #IF {$loaded_classes_search > 0}
        {
            #CLASS {%1} kill;
            #CLASS {%1} {read} {classes/%1.tin};
            #SHOWME Class <069>%1<099> has been reloaded.;
        }
        {
            #SHOWME <019>Class <069>%1<019> is not loaded.<099>;
        }
        #UNVAR loaded_classes_search;
    }
};

#ALIAS {killclass %1}
{
    #IF {"%1" == "all"}
    {
        #IF {&{loaded_classes} && &loaded_classes[]}
        {
            #FOREACH {$loaded_classes[%*]} {loaded_classes_class}
            {
                #CLASS {$loaded_classes_class} kill;
                #SHOWME Class <069>$loaded_classes_class<099> has been killed.
            }
            #UNVAR loaded_classes_class;
            #UNVAR loaded_classes;
        }
        {
            #SHOWME <019>No classes are loaded.<099>;
        }
    }
    {
        #LIST loaded_classes find %1 loaded_classes_search;
        #IF {$loaded_classes_search > 0}
        {
            #CLASS {%1} kill;
            #LIST loaded_classes delete $loaded_classes_search;
            #SHOWME Class <069>%1<099> has been killed.;
        }
        {
            #SHOWME <019>Class <069>%1<019> is not loaded.<099>;
        }
        #UNVAR loaded_classes_search;
    }
};

#ACTION {^Press RETURN to continue.$}
{
    #DELAY {0.5} {#SEND;};
}
{5};

#ACTION {{^NOTE: ([\w\-\'\.]+) is a member of your account and is in-game but disconnected.}}
{
    #SEND %2;
}
{5};

#TICKER           {idle}  {#SEND}  {300};

#VARIABLE         {MAP_LINES}  {0};
#VARIABLE         {MAP_ROWS}  {0};

#CLASS core close;

@kill_all_modules{};

@load_user_config{};

#CLASS utils kill;
#CLASS utils open;
#READ utils.tin;
#CLASS utils close;

@create_layout{};

#VAR is_startup 1;
#MAP flag vtmap off;
@load_all_modules{};
#UNVAR is_startup;
